plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
}

group = 'goorm.eagle7'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-neo4j'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'  //actuator 추가

    implementation 'io.micrometer:micrometer-registry-prometheus'   //마이크로미터 프로메테우스 구현체

    //database driver
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //api document automation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    //mokito
    testImplementation 'org.mockito:mockito-core'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // ⭐ Spring boot 3.x이상에서 QueryDsl 패키지를 정의하는 방법
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    //ioutils
    implementation 'commons-io:commons-io:2.11.0'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'


    //html sanitizer
    // https://mvnrepository.com/artifact/com.googlecode.owasp-java-html-sanitizer/owasp-java-html-sanitizer
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20220608.1'

    //s3 sdk
    implementation 'software.amazon.awssdk:s3:2.17.0'

    // loki4j
    implementation 'com.github.loki4j:loki-logback-appender:1.4.2'

}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

// === ⭐ QueryDsl 빌드 옵션 (선택) ===
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main.java.srcDirs += [querydslDir]
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

clean.doLast {
    file(querydslDir).deleteDir()
}

// jacoco 설정
jacoco {
    // JaCoCo 버전
    toolVersion = "0.8.11"

    //  테스트결과 리포트를 저장할 경로 변경
    //  default는 "$/jacoco"
    //  reportsDir = file("$buildDir/customJacocoReportDir")
}

/**
 * 바이너리 커버리지 결과를 사람이 읽기 좋은 형태의 리포트로 저장합니다.
 * html 파일로 생성해 사람이 쉽게 눈으로 확인할 수도 있고,
 * SonarQube 등으로 연동하기 위해 xml, csv 같은 형태로도 리포트를 생성할 수 있습니다.
 */
jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [ // 필터링할 파일 목록
                               '**/dto/**',
                               '**/Q*',
                               'goorm/eagle7/stelligence/api/**',
                               'goorm/eagle7/stelligence/common/dev/**'
                    ])
        }))
    }
    reports {
        html {
            enabled true
        }
    }
}